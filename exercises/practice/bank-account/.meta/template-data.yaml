properties:
  bankAccount:
    test: |-
      my $final-op = %case<input><operations>[*-1];
      my $lives = %case<input><operations>[0..*-2].map({ '            $account.' ~ .<operation> ~ ( .<amount>:exists ?? "({.<amount>})" !! '' ) ~ ';' }).join("\n");
      if $lives {
          $lives = "\n    lives-ok(\n        \{\n" ~ $lives ~ "\n        },\n        'operations',\n    );";
      }
      if 'concurrent' âˆˆ %case<scenarios> {
          'flunk "NYI";'
      }
      elsif %case<expected><error>:exists {
          my $error = do given %case<expected><error> {
              when / 'not open' $/          {'X::BankAccount::Closed'}
              when / 'already open' $/      {'X::BankAccount::AlreadyOpen'}
              when / 'greater than 0' $/    {'X::BankAccount::NotPositive'}
              when / 'less than balance' $/ {'X::BankAccount::NoOverdraft'}
          };
          sprintf(q:to/END/, %case<description>.raku, $lives, $final-op<operation>, ($final-op<amount>:exists ?? "({$final-op<amount>})" !! ''), $error, %case<expected><error>.raku);
          subtest %s => {
              my BankAccount $account.=new;%s
              throws-like(
                  { $account.%s%s },
                  %s,
                  :message(/^ %s $/),
                  'error',
              );
          }
          END
      }
      else {
        sprintf(q:to/END/, %case<description>.raku, $lives, %case<input><operations>[*-1]<operation>, %case<expected>);
        subtest %s => {
            my BankAccount $account.=new;%s
            cmp-ok(
                $account.%s,
                &infix:<eqv>,
                %s,
                'balance',
            );
        }
        END
      }

unit: false
example: |-
  my class X::BankAccount::Closed is Exception {
      method message {'account not open'}
  }

  my class X::BankAccount::AlreadyOpen is Exception {
      method message {'account already open'}
  }

  my class X::BankAccount::NotPositive is Exception {
      method message {'amount must be greater than 0'}
  }

  my class X::BankAccount::NoOverdraft is Exception {
      method message {'amount must be less than balance'}
  }

  class BankAccount {

      has UInt:D $!balance = 0;
      has Bool:D $.is-open = False;

      method balance {
          X::BankAccount::Closed.new.throw unless $.is-open;
          return $!balance;
      }

      method open {
          X::BankAccount::AlreadyOpen.new.throw if $.is-open;
          return $!is-open.=succ;
      }

      method close {
          self!change-balance(-$.balance);
          return $!is-open.=pred;
      }

      method withdraw (Int:D $amount) {
          X::BankAccount::NotPositive.new.throw if $amount < 0;
          return self!change-balance(-$amount);
      }

      method deposit (Int:D $amount) {
          X::BankAccount::NotPositive.new.throw if $amount < 0;
          return self!change-balance($amount);
      }

      method !change-balance ($amount) {
          X::BankAccount::Closed.new.throw unless $.is-open;
          X::BankAccount::NoOverdraft.new.throw if $amount < 0 && $amount.abs > $.balance;
          return $!balance += $amount;
      }

  }


stub: |-
  my class X::BankAccount::Closed is Exception {
      method message {'account not open'}
  }

  class BankAccount {

      has UInt:D $.balance = 0;

      method open {
          return True;
      }

      method close {
          return True;
      }

      method withdraw (Int:D $amount where * > 0) {
      }

      method deposit (Int:D $amount where * > 0) {
      }

      method !change-balance ($amount) {
      }
  }
